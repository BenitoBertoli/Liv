apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

ext.buildToolsVersion = '28.0.1'
ext.minimumSdkVersion = 17
ext.targetSdkVersion = 28
ext.compileSdkVersion = 28
ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 0
ext.versionClassifier = ""
ext.isSnapshot = false

ext {
    bintrayRepo = 'maven'
    bintrayName = 'liv'

    publishedGroupId = 'com.benitobertoli'
    libraryName = 'Liv'
    artifact = 'liv'

    libraryDescription = 'Live Input Validation for Android'

    siteUrl = 'https://github.com/BenitoBertoli/Liv'
    gitUrl = 'https://github.com/BenitoBertoli/Liv.git'

    libraryVersion = generateVersionName()

    developerId = 'benitobertoli'
    developerName = 'Benito Bertoli'
    developerEmail = 'benitogbertoli@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode generateVersionCode()
        versionName generateVersionName()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null && ext.isSnapshot) {
        ext.versionClassifier = "SNAPSHOT"
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.11'
    implementation 'com.jakewharton.rxbinding3:rxbinding-core:3.1.0'
    implementation 'com.jakewharton.rxbinding3:rxbinding-material:3.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    testImplementation("com.google.truth:truth:1.0") {
        exclude group: 'com.google.code.findbugs'
    }
}

apply from: 'install.gradle'
apply from: 'bintray.gradle'
repositories {
    mavenCentral()
}
